
1 REM "MSGCONV"
10 CLEAR 1000
15 DEFINT A-Z
18 OK=-1
20 INPUT "STARTING, ENDING OLD MSG #";SM,EM
22 HT=99:IF EM<99 THEN HT=EM
25 LT=0:IF SM>0 THEN LT=(SM-100*INT(SM/100))AND &HFFFE
50 ON ERROR GOTO 2500
99 REM
100 FOR I=LT TO HT STEP 2       :REM 0, 2, 4, ETC.
105 DR=0:REM DATA WRITTEN
110 GOSUB 3000                  :REM CALC OUTPUT NAME
120 PRINT "O.O. "RIGHT$(O$,3)
130 IF OK THEN  OPEN "O",1,O$
199 REM
200 SH=0:IF SM>99 THEN SH=100*INT(SM/100)
205 FOR J=SH TO EM STEP 100      :REM 1,2 THEN 101,102, ETC
206 IF (I+J)>EM THEN 1500
210 GOSUB 4000                  :REM CALC INPUT NAME
215 IF POS(0)>10 THEN PRINT
220 PRINT TAB(10)"(O"RIGHT$(I$,4)
230 OPEN "I",2,I$
299 S=0
300 FOR K=0 TO 1
305 IF EF THEN EF=0:GOTO 1000
310 PRINT "("I+J+K")"
320 IF (I+J+K)>EM THEN 1000
330 IF (I+J+K)=0 THEN 1000
400 M$=RIGHT$(STR$(1000+I+J+K),3)
405 IF S THEN S=0:L$=S$:GOTO 420
410 LINE INPUT #2,L$
420 IF LEFT$(L$,1)<>CHR$(7) THEN 500
425 IF MID$(L$,2,3)>M$ THEN S$=L$:S=-1:GOTO 1000
430 IF MID$(L$,2,3)<>M$ THEN 500
435 DR=-1:REM DATA WRITTEN
440 PRINT CHR$(7)+"00"+MID$(L$,2)
445 IF OK THEN  PRINT #1,CHR$(7)+"00"+MID$(L$,2)
450 LINE INPUT #2,L$
460 IF LEFT$(L$,1)=CHR$(7) THEN S$=L$:S=-1:GOTO 1000
470 IF OK THEN PRINT #1,L$
480 IF NOT EOF(2) THEN 450
490 GOTO 510
500 IF NOT EOF(2) THEN 410
510 EF=-1
1000 NEXT K
1005 K=0
1010 PRINT " C"RIGHT$(I$,4)")";
1020 CLOSE 2
1500 NEXT J
1510 PRINT "/C.O. "RIGHT$(O$,3)
1520 IF OK THEN CLOSE 1
1530 REM IF NOT OK THEN 2000
1540 IF NOT DR THEN PRINT "KILLED "O$
2000 NEXT I
2010 END
2500 REM GET CONTROL ON FILE NOT FOUND
2505 IF ERR=53 THEN PRINT "NO FILE "I$:GOTO 2510
2507 PRINT "ERROR "ERR
2510 IF ERL=230 THEN CLOSE 2:RESUME 1020
2520 PRINT "ERROR "ERR" AT LINE "ERL
2530 END
3000 REM CALC OUTPUT FILENAME
3010 O$="B:MESSAGE.X"+RIGHT$(STR$(100+I),2):RETURN
4000 REM CALC INPUT FILENAME
4010 I$="MESSAGE."+MID$(STR$(1000+I+J+K),3,2)+"X":RETURN

B>