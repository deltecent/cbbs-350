;CBBS V3.5.0 	CBBSPMMI.ASM - PMMI MODEM
;07/11/81 12:59:41
;	LINKS TO CBBSWORK
;
;	O O O	O   O	O   O	O O O
;	O   O	OO OO	OO OO	  O
; CBBS	O O O	O O O	O O O	  O	.ASM
;	O	O   O	O   O	  O
;	O	O   O	O   O	O O O
;
;MOD LOG: (THRU 3.2 WRITTEN TO "HISTORY")
;
;====> HISTORICAL COMMENTS SINCE 3.3 TO "HISTORY.033"
;
;PMMI MODEM BOARD EQUATES
;
PMMIB	EQU	0C0H	;BASE ADDRESS
;
PMMIUST	EQU	PMMIB	;UART STATUS PORT ADDRESS
;	INPUT BITS IN PMMIUST
PMMIDAV	EQU	2	;DATA AVAILABLE
PMMITBE	EQU	1	;XMIT BUFF EMPTY
;	OUTPUT BITS FOR PMMIUST
PMMONP	EQU	10H	;NO PARITY
PMMONB1	EQU	8	;# BITS 1
PMMINB2	EQU	4	;# BITS 2
PMMIANS	EQU	2	;ANSWER MODE
;	----
PMMIDAT	EQU	PMMIB+1	;DATA PORT ADDRESS
PMMIMST	EQU	PMMIB+2	;MODEM STATUS PORT
;	INPUT BITS IN PMMIMST
PMMICTS	EQU	4	;(NOT) CTS (I.E. CARRIER)
PMMIRI	EQU	2	;(NOT) RING INDICATOR
;
PMMIBRT	EQU	PMMIMST	;BAUD RATE PORT
PMMIMCT	EQU	PMMIB+3	;MODEM CTL OUTPUT
;	OUTPUT BITS FOR PMMIMCT
PMMILS	EQU	7FH	;LOW SPD (<=300)
PMMIHS	EQU	5FH	;HI SPD (>300)
;
PMMI81	EQU	1CH	;8 DATA BITS, 1 STOP
PMMI82	EQU	5CH	;8 DATA BITS, 2 STOP
;
PMMI110	EQU	15625/110 ;VARIOUS..
PMMI300	EQU	15625/300 ;..BAUD..
PMMI450	EQU	15625/450 ;..RATES
PMMI600	EQU	15625/600
;
;WHEN GOING OFF HOOK, THE ANSWER PHONE
;BIT IS TURNED ON, BUT AFTER CARRIER IS
;FOUND IT IS TURNED OFF.  THIS ALLOWS THE
;PMMI TO HANG UP ON LOSS OF CARRIER BY
;ITSELF.  THE BIOS CHARACTER IN/OUT ROUTINES
;TEST FOR LOSS OF CARRIER, AND FOR THE BIT
;SAYING THAT THE MODEM CHIP HAS HUNG UP.
;THIS IS AN AUTOMATIC 15 SECOND LOSS TEST.
;
;ROUTINE TO CONNECT TO THE CALLER
;
CONNECT	EQU	$
	MVI	A,CTLMOTR ;LEAVE DISKETTE..
	OUT	CTLPORT	;..MOTOR ON
;
	MVI	A,PMMILS ;TURN ON DTR
	OUT	PMMIMCT	;TO MODEM CTL PORT
	MVI	A,PMMI82+PMMIANS ;ANS PHONE
	OUT	PMMIUST	;TO UART PORT
;
	MVI	B,15	;APPROX # SEC. DELAY
DELAY	LXI	H,9A00H	;1 SEC DELAY
DELLP	IN	PMMIMST
	ANI	PMMICTS
	JZ	GOTCAR	;EXIT LOOP IF CARRIER FOUND
	DCX	H	;DECR. LOOP COUNT
	MOV	A,H
	ORA	L
	JNZ	DELLP
;DELAYED ANOTHER SECOND, MORE SECONDS?
	DCR	B	;DCR SECONDS
	JNZ	DELAY	;YES
	JMP	NOCAR
;
;LOOK FOR C/R AT EACH SPEED
;
GOTCAR	MVI	A,10+1	;10 TRIES FOR C/R
	STA	TRIES
;
;QUIT IF NO C/R HAS BEEN READ AFTER 10 TRIES
;
TRY1	LXI	H,TRIES
	DCR	M
	JZ	NOCAR	;NO CARRIER
;
	CALL	SET110
01	CZ	CKCR	;GET SECOND C/R
	RZ
	CALL	SET300
01	CZ	CKCR	;GET SECOND C/R
	RZ
	CALL	SET450
01	CZ	CKCR
	RZ
	CALL	SET600
01	CZ	CKCR
	RZ
;
;READ 110 A SECOND TIME.  OTHERWISE 110 C/R READ
;AT 600 WILL FAIL AND CAUSE INVALID 110 C/R TOO
;
02	CALL	SET110
02	CZ	CKCR	;GET SECOND C/R
02	RZ
	JMP	TRY1	;TRY 10 TIMES
;
;TEST FOR C/R ENTERED, OR LOSS OF CARRIER
;
CKCR	CALL	KEYIN
	CPI	CR
	RZ
	INR	A	;FF MEANS NO CARR
	RNZ
;
;NO CARRIER FOUND
;
NOCAR	LXI	H,NOCON
	JMP	PRDIS	;PRINT MSG, EXIT TO PROM
NOCON	DB	CR,LF
	DB	'++NO C/R SEEN, OR LOSS OF CARRIER'
	DB	CR,LF,'    ',0
;
;SET SPEED TO 110 BAUD - PARITY INHIBIT, 2 STOP BITS,
;ANSWER MODE
;
SET110	MVI	A,PMMI82	;8 DATA, 2 STOP, NO PARITY
	OUT	PMMIUST
	MVI	A,PMMILS	;LOW SPEED DTR
	OUT	PMMIMCT		;TO MODEM CTL PORT
	MVI	A,PMMI110
	OUT	PMMIBRT
	MVI	A,'1'
	STA	SPEED
	JMP	CKCR		;CHECK FOR C/R
;
;SET SPEED TO 300 BAUD, PARITY INHIBIT, 1 STOP BIT,
;ANSWER MODE
;
SET300	MVI	A,PMMI81	;8 DATA BITS, 1 STOP, NO PARITY
	OUT	PMMIUST
	MVI	A,PMMI300
	OUT	PMMIBRT
	MVI	A,'3'
	STA	SPEED
	JMP	CKCR
;
SET450	MVI	A,PMMIHS	;HI SPEED DTR
	OUT	PMMIMCT		;TO MODEM CTL
	MVI	A,PMMI450
	OUT	PMMIBRT
	MVI	A,'4'
	STA	SPEED
	JMP	CKCR
;
SET600	MVI	A,PMMI600
	OUT	PMMIBRT
	MVI	A,'6'
	STA	SPEED
	JMP	CKCR
;
;SET BAUD RATE, BINARY VALUE IN C
;(FROM ALTBAUD COMMAND)
;
SETBAUD	MOV	A,C	;GET RATE
	OUT	PMMIBRT
01	CPI	PMMI300		;..IF > 300
01	MVI	A,PMMIHS	;HI SPEED DTR
01	JC	SETB2
01	MVI	A,PMMILS	;LOW SPEED
01 SETB2:
01	OUT	PMMIMCT		;TO MODEM CTL
	CALL	KEYIN	;AWAIT A CHAR
	JMP	FUNCT
;
;ROUTINE TO HANG UP PHONE
;
HANGUP	XRA	A
	OUT	PMMIUST	;ON HOOK
	OUT	PMMIMCT	;KILL DTR
	RET
;
;WAIT FOR PHONE TO RING IF RE-INIT
;
	IF	REINIT
RINGWT	IN	PMMIMST	;INPUT STATUS PORT
	ANI	PMMIRI	;RINGING?
	JZ	START
	IN	SSW	;GO TO START IF REMOTE
	ANI	REMOTE	;..SWITCH TURNED OFF
	JZ	START
	JMP	RINGWT
	ENDIF		;REINIT
;
	LINK	CBBSWORK ;NEXT .ASM FILE
